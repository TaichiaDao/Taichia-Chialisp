;This is a gateway of the project donation. Donors pay XCH directly to this puzzle.
;This puzzle will create two coins, CAT eve coin and fund deposit coin.
;The CAT eve coin needs to be spent with this coin.
(mod
  (
    ;Platform wallet
    PLATFORM_PUZHASH
    ;Project wallet
    PROJECT_PUZHASH
    ;Public key of the project. This key pair only used for minting.
    PROJECT_PUBKEY
    ;Deadline of the project donation
    DEADLINE_TIMESTAMP
    ; Platform share in percentage
    PLATFORM_SHARE
    ;Puzzle hash of the tail program with curried public key
    TAIL_MOD_PUZHASH
    ;Inner puzzle, project owner can customize this in the future.
    inner_puzzle
    ;Donor wallet
    donor_puzhash
    ;ID of this coin
    my_coin_id
    ;Amount of the XCH goes to the fund
    fund_amount
    ;Amount of the XCH used for minting CATs
    mint_amount
  )
  ;Mod puzzle hash of cat.clsp
  (defconstant CAT_MOD_PUZHASH 0x37bef360ee858133b69d595a906dc45d01af50379dad515eb9518abb7c1d2a7a)
  ;Mod puzzle hash of fund_deposit.clsp
  (defconstant FUND_MOD_PUZHASH 0x84da571180118bba53aa14f42d0f6a779c94b7d5bd097ecddb09942e17d86e27)
  (include "condition_codes.clib")
  (include "curry_and_treehash.clib")

  ;main
    (list
      (list ASSERT_MY_COIN_ID my_coin_id)
      (list AGG_SIG_UNSAFE PROJECT_PUBKEY (sha256 my_coin_id fund_amount mint_amount))
      (list CREATE_COIN
        (puzzle-hash-of-curried-function FUND_MOD_PUZHASH
          ;Currying constants in reverse order
          (sha256tree PLATFORM_SHARE)
          (sha256tree DEADLINE_TIMESTAMP)
          (sha256tree donor_puzhash)
          (sha256tree PROJECT_PUBKEY)
          (sha256tree PROJECT_PUZHASH)
          (sha256tree PLATFORM_PUZHASH)
        )
        fund_amount
      )
      (list CREATE_COIN
        (puzzle-hash-of-curried-function CAT_MOD_PUZHASH
          ;Currying constants in reverse order
          (sha256tree inner_puzzle)
          (sha256tree TAIL_MOD_PUZHASH)
          (sha256tree CAT_MOD_PUZHASH)
        )
        mint_amount
      )
    )
)
